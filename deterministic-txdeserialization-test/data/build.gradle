apply plugin: 'kotlin'

configurations {
    testData
}

dependencies {
    testCompile "net.corda.kernel:corda-core:${corda_kernel_version}"
    testCompile project(':finance:workflows')
    testCompile project(':node-driver')
    testCompile project(path: ':deterministic-txdeserialization-test:verifier', configuration: 'runtimeArtifacts')

    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-reflect"

    testImplementation "junit:junit:$junit_version"

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit_vintage_version}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_version}"
}

jar.enabled = false

test {
    filter {
        // Running this class is the whole point, so include it explicitly.
        includeTestsMatching "net.corda.deterministic.data.GenerateData"
    }
    // force execution of these tests to generate artifacts required by other module (eg. VerifyTransactionTest)
    // note: required by Gradle Build Cache.
    outputs.upToDateWhen { false }
}
assemble.finalizedBy test

artifacts {
    testData file: file("$buildDir/test-data.jar"), type: 'jar', builtBy: test
}
